apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: rootProject.file('gradle/git_utils.gradle')
apply from: rootProject.file('gradle/gini_credentials.gradle')

def appVersionCode = gitCommitUnixTime()
def appVersionName = "${version}-${gitBranch()}-${gitHash()} (${appVersionCode})"

task printVersion {
    doLast {
        println "${appVersionName}"
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "net.gini.android.capture.screenapiexample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode appVersionCode
        versionName appVersionName

        // Use the test runner with JUnit4 support
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        multiDexEnabled true
    }

    buildFeatures {
        viewBinding true
    }

    signingConfigs {
        release {
            storeFile file(releaseKeystoreFile)
            storePassword releaseKeystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
        }
    }

    buildTypes {
        debug {
            resValue 'string', 'gini_api_client_id', clientId
            resValue 'string', 'gini_api_client_secret', clientSecret
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue 'string', 'gini_api_client_id', clientId
            resValue 'string', 'gini_api_client_secret', clientSecret
        }
    }
}

// For testing a released version
repositories {
    // Use repo on filesystem for using local builds
    mavenLocal()
    maven {
        url 'https://repo.gini.net/nexus/content/repositories/open'
    }
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':exampleShared')
    implementation "androidx.appcompat:appcompat:$versions.androidxAppCompat"
    implementation "androidx.activity:activity:$versions.androidxActivity"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$versions.androidxCoordinatorLayout"
    implementation "androidx.multidex:multidex:$versions.androidxMultidex"

    implementation 'com.karumi:dexter:6.2.3'

    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }

    // For testing the local version
    implementation project(path: ':ginicapture-network')
    // For testing a released version
    //implementation 'net.gini:gini-capture-network-lib:2.5.0'

    // For testing the local version
    implementation project(path: ':ginicapture-accounting-network')
    // For testing a released version
    //implementation 'net.gini:gini-capture-accounting-network-lib:2.5.0'

    // For testing the local version
    implementation project(path: ':ginicapture')
    // For testing a released version
    //implementation 'net.gini:gini-capture-sdk:2.0.0-alpha.1'

    testImplementation "junit:junit:$versions.junit"

    androidTestImplementation "androidx.test:runner:$versions.androidxTest"
    androidTestImplementation "com.google.truth:truth:$versions.truth"
    androidTestImplementation "androidx.test:rules:$versions.androidxTest"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.androidxTestEspresso"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$versions.androidxTestEspresso"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:$versions.androidxTestUiAutomator"
    androidTestImplementation "org.mockito:mockito-core:$versions.mockito"
    androidTestImplementation "org.mockito:mockito-android:$versions.mockito"
    androidTestImplementation "androidx.multidex:multidex:$versions.androidxMultidex"
    androidTestImplementation "androidx.test.ext:junit:$versions.androidxTestJUnit"
}

apply from: rootProject.file('gradle/codequality.gradle')
apply from: rootProject.file('gradle/multidex_for_tests.gradle')